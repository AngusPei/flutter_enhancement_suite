buildscript {
    ext.kotlin_version = kotlinVersion

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.9'
    id 'de.undercouch.download' version '4.0.0'
}

group 'de.mariushoefler'
version version

apply plugin: 'kotlin'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.kittinunf.fuel:fuel:2.0.1'
    implementation 'com.github.kittinunf.fuel:fuel-gson:2.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.10'
}

[compileKotlin, compileTestKotlin]*.kotlinOptions {
    jvmTarget = javaVersion
    freeCompilerArgs = ["-Xskip-runtime-version-check"]
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    pluginName 'Flutter Enhancement Suite'

    version ideaVersion
    updateSinceUntilBuild false

    plugins = ["kotlin", "yaml", "Dart:$dartVersion", "io.flutter:$flutterVersion"]
}

publishPlugin {
    def authenticationToken = "$System.env.FES_TOKEN"
    token = authenticationToken
}

patchPluginXml {
    changeNotes """
    <h2>1.1.2</h2>
    <ul>
        <li>Fixed compatibility issues with Android Studio 3.4.2</li>
    </ul>
    <h2>1.1.1</h2>
    <ul>
        <li>Added icon previews</li>
        <li>Fixed issue caused by autocompleting pub dependencies</li>
    </ul>
    <h2>1.1.0</h2>
    <ul>
        <li>Added "New Flutter Widget" option to the "New File" menu</li>
        <li>Added "New Flutter Bloc" option to the "New File" menu</li>
        <li>Added even more snippets</li>
        <li>Improved the performance of autocompleting pub dependencies</li>
    </ul>
    <h2>1.0.0</h2>
    <ul>
        <li>Added support for automatically checking for updates in pubspec.yaml</li>
        <li>Added support for autocompleting pub.dev dependencies</li>
        <li>Added snippets</li>
    </ul>
    """
}

task downloadIntellijPluginVerifier(type: Download) {
    src "https://dl.bintray.com/jetbrains/intellij-plugin-service/org/jetbrains/intellij/plugins/verifier-cli/$pluginVerifierVersion/verifier-cli-$pluginVerifierVersion-all.jar"
    dest buildDir
    overwrite false
}

task listPlugins(type: Exec) {
    workingDir = buildDir
    executable = "sh"
    args = ["-c", "ls ../releases/release_master/*/*.zip >release.list"]
}

task verifyPluginCompatibility(type: Exec, dependsOn: ["downloadIntellijPluginVerifier"]) {
    workingDir = buildDir
    executable = "sh"
    args = ["-c", "java -jar verifier-cli-$pluginVerifierVersion-all.jar check-plugin distributions/flutter-intellij-$version\\.zip ~/.gradle/caches/modules-2/files-2.1/com.jetbrains.intellij.idea/ideaIC/LATEST-EAP-SNAPSHOT/80852e6b02067b26385f5f0ef8e2174f0cd4576/ideaIC-LATEST-EAP-SNAPSHOT"]
/*  args = ["-c", "java -jar verifier-cli-$pluginVerifierVersion-all.jar check-plugin @release.list ~/.gradle/caches/modules-2/files-2.1/com.jetbrains.intellij.idea/ideaIC/LATEST-EAP-SNAPSHOT/80852e6b02067b26385f5f0ef8e2174f0cd4576/ideaIC-LATEST-EAP-SNAPSHOT"]*/
}

verifyPlugin {
    pluginDirectory "resources/META-INF"
    ignoreFailures true
}

check.dependsOn verifyPluginCompatibility
check.dependsOn verifyPlugin